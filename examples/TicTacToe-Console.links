#tictactoe console edition

typename Player = [|X|O|];
typename Option(a) = [|None|Some:a|];

var initialState = [None, None, None, None, None, None, None, None, None] : [Option(Player)];

sig addMove : (Player, Int, [Option(Player)]) ~> [Option(Player)]
fun addMove(player, index, state){
    print("Set Move");
    mapi(fun(field, i){
        if(i==index){
            Some(player)
        } else{
            field
        }
    }, state)
}

sig x: ([Option(Player)]) {hear:Int|_} ~> ()
fun x(gameState){
    var index = recv();
    switch(gameState !! index){
        case None -> {
            print("Setting X.");
            var newGameState = addMove(X, index, gameState);
            printState(newGameState);
            if (checkIfWon(X, newGameState))
                print("X won")
            else
                o(newGameState)
            
        }
        case Some(player) -> {
           print("Already full") 
        }
    }
}

sig o: ([Option(Player)]) {hear:Int|_} ~> ()
fun o(gameState){
    var index = recv();
    switch(gameState !! index){
        case None -> {
            print("Setting O.");
            var newGameState = addMove(O, index, gameState);
            printState(newGameState);
            if (checkIfWon(O, newGameState))
                print("O won")
            else
                x(newGameState)
                    
        }
        case Some(player) -> {
           print("Already full") 
        }
    }
}

sig checkIfWon : (Player, [Option(Player)]) ~> Bool
fun checkIfWon(player : Player, gameState){

    # very stupid. Couldn't understand how to loop through by rows and columns
    var zero = gameState !! 0;
    var one = gameState !! 1;
    var two = gameState !! 2; 
    var three = gameState !! 3;
    var four = gameState !! 4;
    var five = gameState !! 5;
    var six = gameState !! 6;
    var seven = gameState !! 7;
    var eight = gameState !! 8;
    (zero == one) && (one == two) && (two == Some(player)) ||
    (three == four) && (four == five) && (five == Some(player)) ||
    (six == seven) && (seven == eight) && (eight == Some(player)) ||
    (zero == three) && (three == six) && (six == Some(player)) ||
    (one == four) && (four == seven) && (seven == Some(player)) ||
    (two == five) && (five == eight) && (eight == Some(player)) ||
    (zero == four) && (four == eight) && (eight == Some(player))||
    (two == four) && (four == six) && (six == Some(player))

}

sig playerToInt : (Player) -> Int
fun playerToInt(player) {
    switch(player){
        case X -> 0
        case O -> 1
    }
}

sig playerToString : (Player) -> String
fun playerToString(player) {
    switch(player){
        case X -> "X"
        case O -> "O"
    }
}

sig optionToString : ((a) -e-> String) -> (Option(a)) -e-> String
fun optionToString(xToString)(option){
    switch(option){
        case None -> "_"
        case Some(player) -> xToString(player)
    }
}

sig listToString : ([a], ((a) ~e~> String)) ~e~>  String 
fun listToString(list, xToString){
   "[" ^^ stringCat(intersperse(", ", map(xToString, list))) ^^ "]"
}

sig printState : ([Option(Player)]) ~> () 
fun printState(state){
    print(listToString(state, optionToString(playerToString)))
}

sig intersperse : (a, [a]) ~> [a]
fun intersperse(y, xs){
    switch(xs){
        case [] -> []
        case [x] -> [x]
        case x::xs -> x::y::intersperse(y, xs)
    }
}

sig stringCat : ([String]) ~> String
fun stringCat(xs){
    switch(xs){
        case [] -> ""
        case x::xs -> x ^^ stringCat(xs)
    }
}

sig playGame : ([Int]) ~> ()
fun playGame(moveList){
    var gameId = spawnAngel{x(initialState)};
    fun nextMove(moveList){
        switch(moveList){
            case [] -> ()
            case move::moveList -> {
                gameId ! move;
                nextMove(moveList)
            }
        }
    }
    nextMove(moveList)
}