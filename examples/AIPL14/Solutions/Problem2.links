# SMTP 2 (with MAIL recursion)

typename Address = String;
typename Message = String;
typename Error = String;
typename Accept = String;

typename SMTPServer =
  mu send.[&|MAIL:?Address.
                  [+|REJECT:!Error.EndBang,
                     ACCEPT:!Accept.
                            [&|RCPT:?Address.
                                    [+|REJECT:!Error.EndBang,
                                       ACCEPT:!Accept.
                                              [&|DATA:?Message.!Accept.send|&] |+] |&] |+],
             QUIT:EndBang|&];

sig mailServer : (SMTPServer) ~> EndBang
fun mailServer(s) {
  offer(s) {
    case MAIL(s) ->
      var (address, s) = receive(s);
      print("S: received address as FROM: " ^^ address);
      if (valid(address)) {
        var s = send("250 OK", select ACCEPT s);
        offer(s) {
          case RCPT(s) ->
            var (address, s) = receive(s);
            print("S: received address as TO: " ^^ address);
            if (valid(address)) {
              var s = send("250 OK", select ACCEPT s);
              offer(s) {
                case DATA(s) ->
                  var (message, s) = receive(s);
                  print("S: received message: " ^^ message);
                  var s = send("250 OK", s);
                  mailServer(s)
              }
            } else {
              var s = send("Invalid e-mail", select REJECT s);
              s
            }
        }
      } else {
        var s = send("Invalid e-mail", select REJECT s);
        s
      }
    case QUIT(s) ->
      s
  }
}

# Returns true, if the given String is a valid e-mail address.
# The bit before @ has to be a single word!
sig valid : (String) ~> Bool
fun valid(address) {
  address =~ /.[a-z0-9]+.@.[a-z]+.[a-z]+/ # Regex to match 1+characters/numbers @ 1+characters . 1+characters
}


sig mailClient : (~SMTPServer) ~> ()
fun mailClient(c) {
  var c = send("starlight@domain.com", select MAIL c);
  offer (c) {
    case REJECT(c) ->
      var (error, c) = receive(c);
      print("C: client sent MAIL, server replied: " ^^ error);
      wait(c);
      ()

    case ACCEPT(c) ->
      var (reply, c) = receive(c);
      print("C: client sent MAIL, server replied: " ^^ reply);

      var c = send("pink@cloud", select RCPT c);
      offer (c) {
        case REJECT(c) ->
          var (error, c) = receive(c);
          print("C: client sent RCPT, server replied: " ^^ error);
          wait(c);
          ()
        case ACCEPT(c) ->
          var (reply, c) = receive(c);
          print("C: client sent RCPT, server replied: " ^^ reply);
          var c = send("Hello to bravest warriors!", select DATA c);
          var (reply, c) = receive(c);
          print("C: client sent DATA, server replied: " ^^ reply);

          ### To show recursion works.

          var c = send("jane@feathers.com", select MAIL c);
          offer (c) {
            case REJECT(c) ->
              var (error, c) = receive(c);
              print("C: client sent MAIL, server replied: " ^^ error);
              wait(c);
              ()

            case ACCEPT(c) ->
              var (reply, c) = receive(c);
              print("C: client sent MAIL, server replied: " ^^ reply);

              var c = send("cherry@blossom", select RCPT c);
              offer (c) {
                case REJECT(c) ->
                  var (error, c) = receive(c);
                  print("C: client sent RCPT, server replied: " ^^ error);
                  wait(c);
                  ()
                case ACCEPT(c) ->
                  var (reply, c) = receive(c);
                  print("C: client sent RCPT, server replied: " ^^ reply);
                  var c = send("Are we still meeting tonight?", select DATA c);
                  var (reply, c) = receive(c);
                  print("C: client sent DATA, server replied: " ^^ reply);

                  ###

                  var c = select QUIT c;
                  wait(c);
                  ()
              }
          }
      }
  }
}

fun main() {
  mailClient(fork(mailServer))
}

main()
