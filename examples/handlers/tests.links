# Utility functions
fun intersperse(y, xs) {
   switch (xs) {
     case []      -> []
     case [x]     -> [x]
     case x :: xs -> x :: y :: intersperse(y,xs)
   }
}

fun boolToString(b) { if (b) "true" else "false" }

fun pairToString(fstToString, sndToString)(pair) {
  "(" ^^ fstToString(pair.1) ^^ ", " ^^ sndToString(pair.2) ^^ ")"
}

fun listToString(elemToString)(xs) {
  var xs = fold_left(fun (acc, x) { acc ^^ x }
                    , ""
                    , intersperse(",", map(elemToString, xs)));
  "[" ^^ xs ^^ "]"
}

fun assert(equals, expected, actual, toString, testName) {
  if (equals(expected,actual)) ()
  else error("Test failure (" ^^ testName ^^ ") expected `" ^^ toString(expected) ^^ "`; got `" ^^ toString(actual) ^^ "`")
}


# State tests
fun stateExamplesEquals((x,xs),(y,ys)) {
  x == y && xs == ys
}

var stateExamplesToString = pairToString(intToString, listToString(intToString));

import Shallow_state;
fun shallow_state() {
  assert(stateExamplesEquals, (0, [0,1,2,3,4]), Shallow_state.unitTest(5), stateExamplesToString, "Shallow_state")
}

import Deep_state;
fun deep_state() {
  assert(stateExamplesEquals, (0, [0,1,2,3,4]), Deep_state.unitTest(5), stateExamplesToString, "Deep_state")
}

# Pipes tests

import Pipes;
fun deep_pipes() {
  assert((==), [256, 256, 256, 256], Pipes.unitTest(), listToString(intToString), "Deep_pipes")
}

# Exceptions tests
import Exceptions;
fun exceptions() {
  assert((==), 60, Exceptions.unitTest(), intToString, "Exceptions")
}

# Fringe tests
import Fringe;
fun fringe() {
  assert((==), [true, false, false, false, true, true, false, false, true, true], Fringe.unitTest(), listToString(boolToString), "Fringe")
}

# Parameterised handlers
import Light_switch;
fun light_switch() {
  assert((==), true, Light_switch.unitTest(6), boolToString, "Light_switch")
}

# Satisfiability and parsing
import Sat;
fun sat() {
  assert((==), [true, true], Sat.unitTest(), listToString(boolToString), "Sat")
}

# Pi estimation
import Pi;
fun pi() {
  assert((==), true, all(fun(pi) { pi > 3.0 && pi < 4.0 }, Pi.unitTest()), boolToString, "Pi")
}

# Delimited continuations
import Shiftreset;
fun delim() {
  assert((==), [1,2,3], Shiftreset.example(), listToString(intToString), "Shift/reset")
}

# Test runner
fun runTests() {
   var tests = [ shallow_state
               , deep_state
               , deep_pipes
               , exceptions
               , fringe
               , light_switch
               , pi
               , delim
               ];
   ignore(map(fun(t) { t() }, tests))
}

runTests()
