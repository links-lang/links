typename CalcSelect = [+|Add:!Int.!Int.?Int.End, Mul:!Int.!Int.?Int.End, Sub:!Int.!Int.?Int.End, Div:!Int.!Int.?Int.End|+];
typename CalcOffer = [&|Add:?Int.?Int.!Int.End, Mul:?Int.?Int.!Int.End, Sub:?Int.?Int.!Int.End, Div:?Int.?Int.!Int.End|&];

sig calc : (CalcOffer) ~> ()
fun calc(s) client {
  offer (s) {
    case Add(s) ->
      var (x,s) = receive(s);
      var (y,s) = receive(s);
      var _ = send(x+y,s);
      ()
    case Mul(s) ->
      var (x,s) = receive(s);
      var (y,s) = receive(s);
      var _ = send(x*y,s);
      ()

    case Sub(s) ->
      var (x,s) = receive(s);
      var (y,s) = receive(s);
      var _ = send(y-x,s);
      ()
    case Div(s) ->
      var (x,s) = receive(s);
      var (y,s) = receive(s);
      var _ = send(y/x,s);
      ()
  }
}

sig user : (CalcSelect, Int, Int, String) ~> Int
fun user(s, op1, op2, operation) client {

	switch(operation) {
		case "add" ->  { var s = select Add s; receive(send(op1,send(op2,s))).1 }
		case "mult" -> { var s = select Mul s; receive(send(op1,send(op2,s))).1 }
		case "sub" -> { var s = select Sub s; receive(send(op1,send(op2,s))).1 }
		case "div" -> { var s = select Div s; receive(send(op1,send(op2,s))).1 }
	}	 		
}

sig main : (String, String, String) ~> Int
fun main(op1, op2, operation) client {
  user(knife(calc), stringToInt(op1), stringToInt(op2), operation)
}

page
  <html>
  <head><title>[10] Cool Calculator </title></head>
  <body>
  <h3>Cool Integer Calculator</h3> 

   <form l:onsubmit="{domReplaceChildren(stringToXml(intToString(main(op1, op2, operation))), getNodeById("result"))}">
    <input type="text" size="5" l:name="op1"/>
    <select l:name="operation">
 	<option value="add">+</option>
  	<option value="mult">*</option>
	<option value="sub">-</option>
  	<option value="div">/</option>
   </select> 
    <input type="text" size="5" l:name="op2"/>
    <button type="submit">Get Result!</button>
   </form>		  

   <p>The result is:</p>
   <p id="result"></p>

  </body>
  </html>
