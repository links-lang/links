open VirtualDom
open Tags
open Attrs
open Events

typename Entry = (description : String, completed : Bool, editing : Bool, id : Int);
typename Model = (entries : [Entry], field : String, uid : Int, visibility : String);
typename Msg = [| NoOp | UpdateField : String | EditingEntry : (Int,Bool) | UpdateEntry : (Int,String) | Add | Delete : Int | DeleteComplete | Check : (Int,Bool) | CheckAll : Bool | ChangeVisibility : String |];

fun getKeycode(f) {
  StringHandler("keycode", f)
}

fun updateFieldHandler() {
  onInput(fun (str) { UpdateField(str) })
}

fun enterHandler() {
  getKeycode(fun(keycode) { 
    if (keycode == "13") {
      Add
    } else {
      NoOp
    }
  })
}

fun updateEntryHandler(id) {
  onInput(fun(str) { UpdateEntry(id, str) })
}

fun blurHandler(id, tf) {
  onBlur(fun() { EditingEntry(id, tf) })
}

fun editingEnterHandler(id, tf) {
  getKeycode(fun(keycode) { 
    if (keycode == "13") {
      EditingEntry(id, tf)
    } else {
      NoOp
    }
  }) 
}

fun checkEntryHandler(id, notCompleted) {
  onClick(fun() { Check(id, notCompleted) })
}

fun checkAllHandler(notCompleted) {
  onClick(fun() { CheckAll(notCompleted) })
}

fun deleteHandler(id) {
  onClick(fun() { Delete(id) })
}

fun changeVisibilityHandler(vis) {
  onClick(fun() { ChangeVisibility(vis) })
}

fun editingHandler(id, tf) {
  onDoubleClick(fun () { EditingEntry(id, tf) })
}

sig newEntry : (String,Int) -> Entry
fun newEntry(desc, idToSet) {
  (description = desc, completed = false, editing = false, id = idToSet) : Entry
}

fun getUpdatedEntries(model) {
  if (model.field == "") {
    model.entries
  } else {
    model.entries++[newEntry(model.field, model.uid)]
  }
}

sig updt : (Msg, Model) ~%e~> Model
fun updt(msg, model) {
  switch (msg) {
    case NoOp ->
      model

    case Add ->
      (model with uid = model.uid + 1, field = "", entries = getUpdatedEntries(model))

    case UpdateField(str) ->
      (model with field = str)

    case UpdateEntry(id,task) ->
      var updateEntry = fun(currentEntry) {
        if (currentEntry.id == id) {
          (currentEntry with description = task)
        } else {
          currentEntry
        }
      };
      (model with entries = map(updateEntry, model.entries))

    case Delete(id) ->
      (model with entries = filter(fun(t) { t.id <> id }, model.entries))

    case Check(id, isCompleted) ->
      var updateEntry = fun(currentEntry) {
        if (currentEntry.id == id) {
          (currentEntry with completed = isCompleted)
        } else {
          currentEntry
        }
      };
      (model with entries = map(updateEntry, model.entries))

    case CheckAll(isCompleted) ->
      var updateEntry = fun(currentEntry) {
        (currentEntry with completed = isCompleted)
      };
      (model with entries = map(updateEntry, model.entries))   

    case ChangeVisibility(vis) ->
      (model with visibility = vis)  

    case EditingEntry(id, isEditing) ->
      var updateEntry = fun(currentEntry) {
        if (currentEntry.id == id) {
          (currentEntry with editing = isEditing)
        } else {
          currentEntry
        }
      };
      (model with entries = map(updateEntry, model.entries))

    case other ->
      model
  }
}

#sig viewInput : String -> Doc(Msg)
fun viewInput(task) {
  header ([class("header")], [], [
    h1 ([], [], [textDoc("todos")]),
    input ([type ("text"), class ("new-todo"), placeholder ("What needs to be done?"), name ("newTodo"), id ("newTodo"), value(task)], 
    [updateFieldHandler(), enterHandler()], [])
  ])
}

fun viewEntry(todo) {
  var classes = classList([("completed", todo.completed),("editing",todo.editing)]);
  li ([class (classes)],[],[
    div ([class ("view")], [], [
      input ([type ("checkbox"), name ("toggle"), class ("toggle"), checked (todo.completed)], [checkEntryHandler(todo.id, not(todo.completed))], []),
      label ([],[editingHandler(todo.id, true)],[textDoc(todo.description)]),
      span ([class ("destroy")], [deleteHandler(todo.id)], [])
    ]),
    input ([class ("edit"), value (todo.description), name ("title"), id ("todo-" ^^ intToString(todo.id))],
    [updateEntryHandler(todo.id), blurHandler(todo.id, false), editingEnterHandler(todo.id, false)],[])
  ])
}

sig viewKeyedEntry : (Entry) ~> Doc(Msg)
fun viewKeyedEntry(todo) {
  viewEntry(todo)
}

sig viewEntries : (String, [Entry]) ~> Doc(Msg)
fun viewEntries(visibility, entries) {
  var isVisible = fun (todo) {
    switch(visibility) {
      case("Completed") ->
        todo.completed
      case("Active") ->
        not(todo.completed)
      case other ->
        true
    }
  };

  var incompleteList = for (x <- entries) { if (x.completed == false) { [x.completed] } else { [] } };
  var allCompleted = if (length(incompleteList) == 0) {
    true
  } else {
    false
  };

  var cssVisibility = if (length(entries) == 0) {
      "visibility:hidden"
    } else {
      "visibility:visible"
    };
  
  section ([class ("main"), style (cssVisibility)], [], [
    input ([type ("checkbox"), id ("toggle-all"), class ("toggle-all")], [checkAllHandler(not(allCompleted))], []),
    ul ([class ("todo-list")],[],map(viewKeyedEntry, filter(isVisible, entries)))
  ])
}

sig viewControlsCount : (Int) -> Doc(Msg)
fun viewControlsCount(entriesLeft) {
  var item_ = if (entriesLeft == 1) {
    " item"
  } else {
    " items"
  };
  span ([class ("todo-count")], [], [
    strong ([], [], [textDoc(intToString(entriesLeft))]),
    textDoc(item_),textDoc(" left")
  ])
}

sig visibilitySwap : (String, String, String) ~> Doc(Msg)
fun visibilitySwap(uri, visibility, actualVisibility) {
  var classes = classList([("selected", visibility == actualVisibility)]);
  li ([], [changeVisibilityHandler(visibility)], [
    a ([href (uri), class (classes)],[],[textDoc(visibility)])
  ])
}

sig viewControlsFilters : (String) ~> Doc(Msg)
fun viewControlsFilters(visibility) {
 ul ([class ("filters")], [], [
    visibilitySwap("#/", "All", visibility), textDoc(" "),
    visibilitySwap("#/active", "Active", visibility), textDoc(" "),
    visibilitySwap("#/completed", "Completed", visibility)
  ])
}

sig viewControls : (String, [Entry]) ~> Doc(Msg)
fun viewControls(visibility, entries) {
  var entriesCompleted = length(for (x <- entries) { if (x.completed == true) { [x.completed] } else { [] } });
  var entriesLeft = length(entries) - entriesCompleted;
  var entriesEmpty = if (length(entries) == 0) {
    true
  } else {
    false
  };
  footer ([class ("footer"), hidden(entriesEmpty)], [], [
    viewControlsCount(entriesLeft),
    viewControlsFilters(visibility)
  ])
}

sig view : (Model) ~%e~> Doc(Msg)
fun view(model) {
  div ([class ("todomvc-wrapper")], [], [
    section ([class ("todoapp")], [], [
      viewInput(model.field),
      viewEntries(model.visibility, model.entries),
      viewControls(model.visibility, model.entries)
    ])
  ])
}


fun mainPage(_, _) {
  var initialModel = (entries = [], field = "", uid = 0, visibility = "All");
  var evtHandler = runVDom("placeholder", view, updt, initialModel);
  page
  <html>
    <head>
      <title>Links - TodoMVC</title>
      <link rel="stylesheet" href="css/style.css" type="text/css" />
    </head>

    <body>
      <div id="placeholder"></div>
    </body>
  </html>
}

fun main() {
  addRoute("/", mainPage);
  addStaticRoute("/css", "css", [("css", "text/css")]);
  servePages()
}

main()
