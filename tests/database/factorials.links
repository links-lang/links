var db = database "links";
var factorials = table "factorials" with (i : Int, f : Int) from db;

fun deleteAll() {
  delete (x <-- factorials);
}

fun insertNone() {
  insert factorials
  values (i, f)
    [];
}

fun insertOne() {
  insert factorials
  values [(f=1, i=1)];
}

fun insertTwoThree() {
  insert factorials
  values (f, i)
    [(f=2, i=2),
     (f=6, i=3)];
}

fun insertReturningOne() {
  insert factorials
  values [(f=1, i=1)] returning i
}

fun insertReturningTwo() {
  insert factorials
  values (f,i) [(f=2, i=2)] returning i
}

fun updateTwoThree() {
  update (x <-- factorials)
  where (x.i == 2)
  set (f=3);
}

fun lookupFactorials(n) server {
  query {
    for (row <-- factorials)
    where (row.i <= n)
    orderby (row.i)
      [(i=row.i, f=row.f)]
  }
}

fun unwrappedLookup(n) server {
  for (row <-- factorials)
  where (row.i <= n)
  orderby (row.i)
    [(i=row.i, f=row.f)]
}

fun test() {
  insertOne();
  deleteAll();
  assertEq(lookupFactorials(10), []);
  insertNone();
  assertEq(lookupFactorials(10), []);
  insertOne();
  assertEq(lookupFactorials(1), [(f=1,i=1)]);
  assertEq(unwrappedLookup(1), [(f=1,i=1)]);
  insertTwoThree();
  ## The order is wrong.
  assertEq(lookupFactorials(3), [(i=3, f=6), (i=2, f=2), (f=1,i=1)]);
  deleteAll();
  assertEq(lookupFactorials(10), []);
  assertEq(insertReturningOne(), 1);
  assertEq(lookupFactorials(10), [(f=1,i=1)]);
  assertEq(insertReturningTwo(), 2);
  assertEq(lookupFactorials(10), [(f=2,i=2),(f=1,i=1)]);
  updateTwoThree();
  assertEq(lookupFactorials(10), [(f=3,i=2),(f=1,i=1)]);
}

test()
