# Adapted from examples/handlers/choose.links

# Choice example.

# The choice computation
fun choice() {
  var x = if (do Choose()) { 2 }
          else { 4 };
  var y = if (do Choose()) { 8 }
          else { 16 };
  x + y
}

# Linear version of Choose
fun lin_choice() {
  var x = if (lindo Choose()) { 2 }
          else { 4 };
  var y = if (lindo Choose()) { 8 }
          else { 16 };
  x + y
}

#
# A few (closed) handlers
#

# The "positive" handler
fun positive(m)() {
  handle(m()) {
    case x   -> x
    case <Choose =@ k> -> xlin; k(true)
  }
}

# The "I'll-take-everything" handler
fun enumerate(m)() {
  handle(m()) {
    case x -> [x]
    case <Choose => k> -> k(true) ++ k(false)
  }
}

[positive(lin_choice)()] ++ enumerate(choice)()