open Utility
open CommonTypes
open Var

let internal_error message =
  Errors.internal_error ~filename:"query/query.ml" ~message

let runtime_type_error error =
  internal_error
    ("Runtime type error: " ^ error ^ ".\n" ^
     "This should not happen if the type system / checker is correct. " ^
     "Please file a bug report.")

module Lang =
struct

  type base_type = | Bool | Char | Float | Int | String

  type tag = int
      [@@deriving show]

  type t =
      | For       of tag option * (Var.var * t) list * t list * t
      | If        of t * t * t
      | Table     of Value.table
      | Database  of (Value.database * string)
      | Singleton of t
      | Concat    of t list
      | Record    of t StringMap.t
      | Project   of t * string
      | Erase     of t * StringSet.t
      | Variant   of string * t
      | XML       of Value.xmlitem
      | Apply     of string * t list
      | Closure   of (Ir.var list * Ir.computation) * env
      | Primitive of string
      | Var       of Var.var * Types.datatype StringMap.t
      | Constant  of Constant.t
  and env = Value.env * t Env.Int.t
      [@@deriving show]

end

module Q = Lang

let _show_env = Q.show_env (* Generated by ppx_deriving show *)

let unbox_xml =
  function
    | Q.XML xmlitem -> xmlitem
    | _ -> raise (runtime_type_error "failed to unbox XML")

let unbox_pair =
  function
    | Q.Record fields ->
        let x = StringMap.find "1" fields in
        let y = StringMap.find "2" fields in
          x, y
    | _ -> raise (runtime_type_error "failed to unbox pair")

let rec unbox_list =
  function
    | Q.Concat vs -> concat_map unbox_list vs
    | Q.Singleton v -> [v]
    | _ -> raise (runtime_type_error "failed to unbox list")

let unbox_string =
  function
    | Q.Constant (Constant.String s) -> s
    | (Q.Concat _ | Q.Singleton _) as v ->
        implode
          (List.map
             (function
                | Q.Constant (Constant.Char c) -> c
                | _ -> raise (runtime_type_error "failed to unbox string"))
             (unbox_list v))
    | _ -> raise (runtime_type_error "failed to unbox string")

(** Returns which database was used if any.

   Currently this assumes that at most one database is used.
*)
let used_database v : Value.database option =
  let rec generators =
    function
      | [] -> None
      | (_x, source)::gs ->
          begin
            match used source with
              | None -> generators gs
              | Some db -> Some db
          end
  and used =
    function
      | Q.For (_, gs, _, _body) -> generators gs
      | Q.Table ((db, _), _, _, _) -> Some db
      | _ -> None in
  let rec comprehensions =
    function
      | [] -> None
      | v::vs ->
          begin
            match used v with
              | None -> comprehensions vs
              | Some db -> Some db
          end
  in
    match v with
      | Q.Concat vs -> comprehensions vs
      | v -> used v

module S =
struct

  (** [pt]: A printable version of [t] *)
  type pt =
    | For       of (Var.var * pt) list * pt list * pt
    | If        of pt * pt * pt
    | Table     of Value.table
    | Singleton of pt
    | Concat    of pt list
    | Record    of pt StringMap.t
    | Project   of pt * string
    | Erase     of pt * StringSet.t
    | Variant   of string * pt
    | XML       of Value.xmlitem
    | Apply     of string * pt list
    | Lam       of Ir.var list * Ir.computation
    | Primitive of string
    | Var       of (Var.var * Types.datatype StringMap.t)
    | Constant  of Constant.t
      [@@deriving show]

  let rec pt_of_t : Q.t -> pt = fun v ->
    let bt = pt_of_t in
      match (v : Q.t) with
        | Q.For (_, gs, os, b) ->
            For (List.map (fun (x, source) -> (x, bt source)) gs,
                  List.map bt os,
                  bt b)
        | Q.If (c, t, e) -> If (bt c, bt t, bt e)
        | Q.Table t -> Table t
        | Q.Singleton v -> Singleton (bt v)
        | Q.Concat vs -> Concat (List.map bt vs)
        | Q.Record fields -> Record (StringMap.map bt fields)
        | Q.Variant (name, v) -> Variant (name, bt v)
        | Q.XML xmlitem -> XML xmlitem
        | Q.Project (v, name) -> Project (bt v, name)
        | Q.Erase (v, names) -> Erase (bt v, names)
        | Q.Apply (f, vs) -> Apply (f, List.map bt vs)
        | Q.Closure ((xs, e), _) -> Lam (xs, e)
        | Q.Primitive f -> Primitive f
        | Q.Var (v, t) -> Var (v, t)
        | Q.Constant c -> Constant c
        | Q.Database _ -> assert false

  let t = show_pt -<- pt_of_t
end

let string_of_t = S.t

let rec tail_of_t : Q.t -> Q.t = fun v ->
  let tt = tail_of_t in
  let open Q in
    match v with
      | For (_, _gs, _os, Singleton (Record fields)) -> Record fields
      | For (_tag, _gs, _os, If (_, t, Concat [])) -> tt (For (_tag, _gs, _os, t))
      | _ -> (* Debug.print ("v: "^string_of_t v); *) assert false

(** Return the type associated with an expression *)
(* Inferring the type of an expression is straightforward because all
   variables are annotated with their types. *)
let rec type_of_expression : Q.t -> Types.datatype = fun v ->
  let te = type_of_expression in
  let record fields : Types.datatype =
    Types.make_record_type (StringMap.map te fields)
  in
    let open Q in
    match v with
      | Concat [] -> Types.make_list_type(Types.unit_type)
      | Concat (v::_) -> te v
      | For (_, _, _os, body) -> te body
      | Singleton t -> Types.make_list_type (te t)
      | Record fields -> record fields
      | If (_, t, _) -> te t
      | Table (_, _, _, row) -> `Record row
      | Constant (Constant.Bool   _) -> Types.bool_type
      | Constant (Constant.Int    _) -> Types.int_type
      | Constant (Constant.Char   _) -> Types.char_type
      | Constant (Constant.Float  _) -> Types.float_type
      | Constant (Constant.String _) -> Types.string_type
      | Project (Var (_, field_types), name) -> StringMap.find name field_types
      | Apply ("Empty", _) -> Types.bool_type (* HACK *)
      | Apply (f, _) -> TypeUtils.return_type (Env.String.lookup Lib.type_env f)
      | e -> Debug.print("Can't deduce type for: " ^ show e); assert false

let default_of_base_type =
  function
    | Primitive.Bool   -> Q.Constant (Constant.Bool false)
    | Primitive.Int    -> Q.Constant (Constant.Int 42)
    | Primitive.Char   -> Q.Constant (Constant.Char '?')
    | Primitive.Float  -> Q.Constant (Constant.Float 0.0)
    | Primitive.String -> Q.Constant (Constant.String "")
    | _                -> assert false

let rec value_of_expression = fun v ->
  let ve = value_of_expression in
  let value_of_singleton = fun s ->
    match s with
      | Q.Singleton v -> ve v
      | _ -> assert false
  in
    let open Q in
    match v with
      | Constant (Constant.Bool   b) -> `Bool b
      | Constant (Constant.Int    i) -> `Int i
      | Constant (Constant.Char   c) -> `Char c
      | Constant (Constant.Float  f) -> `Float f
      | Constant (Constant.String s) -> Value.box_string s
      | Table t -> `Table t
      | Concat vs -> `List (List.map value_of_singleton vs)
      | Variant (name, v) -> `Variant (name, ve v)
      | XML xmlitem -> `XML xmlitem
      | Record fields ->
          `Record (List.rev (StringMap.fold (fun name v fields ->
                                               (name, ve v)::fields)
                               fields []))
      | _ -> assert false

let rec freshen_for_bindings : Var.var Env.Int.t -> Q.t -> Q.t =
  fun env v ->
    let ffb = freshen_for_bindings env in
      let open Q in
      match v with
      | For (tag, gs, os, b) ->
        let gs', env' =
          List.fold_left
            (fun (gs', env') (x, source) ->
              let y = Var.fresh_raw_var () in
                ((y, ffb source)::gs', Env.Int.bind env' (x, y)))
            ([], env)
            gs
        in
          For (tag, List.rev gs', List.map (freshen_for_bindings env') os, freshen_for_bindings env' b)
      | If (c, t, e) -> If (ffb c, ffb t, ffb e)
      | Table t -> Table t
      | Singleton v -> Singleton (ffb v)
      | Database db -> Database db
      | Concat vs -> Concat (List.map ffb vs)
      | Record fields -> Record (StringMap.map ffb fields)
      | Variant (name, v) -> Variant (name, ffb v)
      | XML xmlitem -> XML xmlitem
      | Project (v, name) -> Project (ffb v, name)
      | Erase (v, names) -> Erase (ffb v, names)
      | Apply (f, vs) -> Apply (f, List.map ffb vs)
      | Closure (a, c) ->
        (* we don't attempt to freshen closure bindings *)
        Closure (a, c)
      | Primitive f -> Primitive f
      | Var (x, ts) ->
        begin
          match Env.Int.find env x with
          | None -> Var (x, ts)
          | Some y -> Var (y, ts)
        end
      | Constant c -> Constant c

let labels_of_field_types field_types =
  StringMap.fold
    (fun name _ labels' ->
      StringSet.add name labels')
    field_types
    StringSet.empty

let record_field_types (t : Types.datatype) : Types.datatype StringMap.t =
  let (field_spec_map, _, _) = TypeUtils.extract_row t in
  StringMap.map (function
                  | `Present t -> t
                  | _ -> assert false) field_spec_map

let table_field_types (_, _, _, (fields, _, _)) =
  StringMap.map (function
                  | `Present t -> t
                  | _ -> assert false) fields

let rec field_types_of_list =
  let open Q in
  function
    | Concat (v::_) -> field_types_of_list v
    | Singleton (Record fields) -> StringMap.map type_of_expression fields
    | Table table -> table_field_types table
    | _ -> assert false

      (* takes a normal form expression and returns true iff it has list type *)
let is_list =
  let open Q in
  function
    | For _
    | Table _
    | Singleton _
    | Concat _
    | If (_, _, Concat []) -> true
    | _ -> false


      (* TODO: Clean up and unify with Queryshredding.Eval *)

module Eval =
struct
  exception DbEvaluationError of string

  let nil = Q.Concat []

  let eval_error fmt =
    let error msg = raise (DbEvaluationError msg) in
      Printf.kprintf error fmt

  let env_of_value_env value_env = (value_env, Env.Int.empty)
  let (++) (venv, eenv) (venv', eenv') =
    Value.Env.shadow venv ~by:venv', Env.Int.extend eenv eenv'

  let lookup_fun (f, fvs) =
    let open Q in
    match Tables.lookup Tables.fun_defs f with
    | Some (finfo, (xs, body), z, location) ->
      Some
      begin
        match Var.name_of_binder (f, finfo) with
        | "concatMap" ->
          Primitive "ConcatMap"
        | "map" ->
          Primitive "Map"
        | "empty" ->
          Primitive "Empty"
        | "sortByBase" ->
          Primitive "SortBy"
        | _ ->
          begin
            match location with
            | Location.Server | Location.Unknown ->
                let env =
                  match z, fvs with
                  | None, None       -> Value.Env.empty
                  | Some z, Some fvs -> Value.Env.bind z (fvs, Scope.Local) Value.Env.empty
                  | _, _ -> assert false in
                Closure ((xs, body), env_of_value_env env)
            | Location.Client ->
              raise (Errors.runtime_error ("Attempt to use client function: " ^
                Js.var_name_binder (f, finfo) ^ " in query"))
            | Location.Native ->
              raise (Errors.runtime_error ("Attempt to use native function: " ^
                Var.show_binder (f, finfo) ^ " in query"))
          end
      end
    | None -> None

  let find_fun (f, fvs) =
    match lookup_fun (f, fvs) with
    | Some v -> v
    | None ->
      raise (internal_error ("Attempt to find undefined function: " ^
        string_of_int f))

  let rec expression_of_value : Value.t -> Q.t =
    let open Q in
    function
      | `Bool b   -> Constant (Constant.Bool b)
      | `Int i    -> Constant (Constant.Int i)
      | `Char c   -> Constant (Constant.Char c)
      | `Float f  -> Constant (Constant.Float f)
      | `String s -> Constant (Constant.String s)
      | `Table t -> Table t
      | `Database db -> Database db
      | `List vs ->
          Concat (List.map (fun v -> Singleton (expression_of_value v)) vs)
      | `Record fields ->
          Record
            (List.fold_left
               (fun fields (name, v) -> StringMap.add name (expression_of_value v) fields)
               StringMap.empty
               fields)
      | `Variant (name, v) -> Variant (name, expression_of_value v)
      | `XML xmlitem -> XML xmlitem
      | `FunctionPtr (f, fvs) -> find_fun (f, fvs)
      | `PrimitiveFunction (f,_) -> Primitive f
      | v ->
          raise (internal_error (Printf.sprintf
              "Cannot convert value %s to expression" (Value.string_of_value v)))


  let bind (val_env, exp_env) (x, v) =
    (val_env, Env.Int.bind exp_env (x, v))

  let lookup (val_env, exp_env) var =
    match lookup_fun (var, None) with
    | Some v -> v
    | None ->
      begin
        match Value.Env.lookup var val_env, Env.Int.find exp_env var with
        | None, Some v -> v
        | Some v, None -> expression_of_value v
        | Some _, Some v -> v (*eval_error "Variable %d bound twice" var*)
        | None, None ->
          begin
            try expression_of_value (Lib.primitive_stub (Lib.primitive_name var)) with
            | NotFound _ ->
                raise (internal_error ("Variable " ^ string_of_int var ^ " not found"));
          end
      end

  let eta_expand_var (x, field_types) =
    Q.Record
      (StringMap.fold
         (fun name _t fields ->
            StringMap.add name (Q.Project (Q.Var (x, field_types), name)) fields)
         field_types
         StringMap.empty)

  let eta_expand_list xs =
    let x = Var.fresh_raw_var () in
    let field_types = field_types_of_list xs in
      ([x, xs], [], Q.Singleton (eta_expand_var (x, field_types)))

  let rec value env : Ir.value -> Q.t = let open Ir in function
    | Constant c -> Q.Constant c
    | Variable var ->
        begin
          match lookup env var with
            | Q.Var (x, field_types) ->
                (* eta-expand record variables *)
                eta_expand_var (x, field_types)
            | Q.Primitive "Nil" -> nil
            (* We could consider detecting and eta-expand tables here.
               The only other possible sources of table values would
               be `Special or built-in functions that return table
               values. (Currently there are no pure built-in functions
               that return table values.)

               Currently eta-expansion happens later, in the SQL
               module.

               On second thoughts, we *never* need to explicitly
               eta-expand tables, as it is not possible to call
               "AsList" directly. The "asList" function in the prelude
               is defined as:

               fun asList(t) server {for (x <-- t) [x]}
            *)
            | v ->
              (* In order to maintain the invariant that each
                 bound variable is unique we freshen all for-bound
                 variables in v here.

                 This is necessary in order to ensure that each
                 instance of a table in a self-join is given a
                 distinct alias, as the alias is generated from the
                 name of the variable binding the table.

                 We are assuming that any closure-bound variables will
                 be eliminated anyway.
              *)
              (* Debug.print ("env v: "^string_of_int var^" = "^string_of_t v); *)
              freshen_for_bindings (Env.Int.empty) v
        end
    | Extend (ext_fields, r) ->
      begin
        match opt_app (value env) (Q.Record StringMap.empty) r with
          | Q.Record fields ->
            Q.Record (StringMap.fold
                       (fun label v fields ->
                         if StringMap.mem label fields then
                           eval_error
                             "Error adding fields: label %s already present"
                             label
                         else
                           StringMap.add label (value env v) fields)
                       ext_fields
                       fields)
          | _ -> eval_error "Error adding fields: non-record"
      end
    | Project (label, r) ->
      let rec project (r, label) =
        match r with
          | Q.Record fields ->
            assert (StringMap.mem label fields);
            StringMap.find label fields
          | Q.If (c, t, e) ->
            Q.If (c, project (t, label), project (e, label))
          | Q.Var (x, field_types) ->
            assert (StringMap.mem label field_types);
            Q.Project (Q.Var (x, field_types), label)
          | _ -> eval_error ("Error projecting from record: %s") (string_of_t r)
      in
        project (value env r, label)
    | Erase (labels, r) ->
      let rec erase (r, labels) =
        match r with
          | Q.Record fields ->
            assert (StringSet.for_all
                      (fun label -> StringMap.mem label fields) labels);
            Q.Record
              (StringMap.fold
                 (fun label v fields ->
                   if StringSet.mem label labels then
                     fields
                   else
                     StringMap.add label v fields)
                 fields
                 StringMap.empty)
          | Q.If (c, t, e) ->
            Q.If (c, erase (t, labels), erase (e, labels))
          | Q.Var (x, field_types) ->
            assert (StringSet.subset labels (labels_of_field_types field_types));
            Q.Erase (Q.Var (x, field_types), labels)
          | _ -> eval_error "Error erasing from record"
      in
        erase (value env r, labels)
    | Inject (label, v, _) -> Q.Variant (label, value env v)
    | TAbs (_, v) -> value env v
    | TApp (v, _) -> value env v

    | XmlNode (tag, attrs, children) ->
        (* TODO: deal with variables in XML *)
        let children =
          List.fold_right
            (fun v children ->
               let v = value env v in
                 List.map unbox_xml (unbox_list v) @ children)
            children [] in
        let children =
          StringMap.fold
            (fun name v attrs ->
               Value.Attr (name, unbox_string (value env v)) :: attrs)
            attrs children
        in
          Q.Singleton (Q.XML (Value.Node (tag, children)))

    | ApplyPure (f, ps) ->
        apply env (value env f, List.map (value env) ps)
    | Closure (f, _, v) ->
      let (_finfo, (xs, body), z_opt, _location) = Tables.find Tables.fun_defs f in
      let z = OptionUtils.val_of z_opt in
      (* Debug.print ("Converting evalir closure: " ^ Var.show_binder (f, _finfo) ^ " to query closure"); *)
      (* yuck! *)
      let env' = bind (Value.Env.empty, Env.Int.empty) (z, value env v) in
      Q.Closure ((xs, body), env')
      (* (\* Debug.print("looking up query closure: "^string_of_int f); *\) *)
      (* begin *)
      (*   match value env (`Variable f) with *)
      (*   | `Closure ((z::xs, body), closure_env) -> *)
      (*     (\* Debug.print("binding query closure parameter: "^string_of_int z); *\) *)
      (*     (\* partially apply the closure to bind the closure *)
      (*        environment *\) *)
      (*     `Closure ((xs, body), bind closure_env (z, value env v)) *)
      (*   | _ -> *)
      (*     failwith "ill-formed closure in query compilation" *)
      (* end *)
    | Coerce (v, _) -> value env v

  and apply env : Q.t * Q.t list -> Q.t = let open Q in function
    | Closure ((xs, body), closure_env), args ->
      (* Debug.print ("Applying closure"); *)
      (* Debug.print ("body: " ^ Ir.show_computation body); *)
      (* Debug.print("Applying query closure: " ^ show_t (`Closure ((xs, body), closure_env))); *)
      (* Debug.print("args: " ^ mapstrcat ", " show_t args); *)
        let env = env ++ closure_env in
        let env = List.fold_right2 (fun x arg env ->
            bind env (x, arg)) xs args env in
        (* Debug.print("Applied"); *)
          computation env body
    | Primitive "AsList", [xs] ->
        xs
    | Primitive "Cons", [x; xs] ->
        reduce_concat [Singleton x; xs]
    | Primitive "Concat", [xs; ys] ->
        reduce_concat [xs; ys]
    | Primitive "ConcatMap", [f; xs] ->
        begin
          match f with
            | Closure (([x], body), closure_env) ->
                let env = env ++ closure_env in
                  reduce_for_source
                    (xs, fun v -> computation (bind env (x, v)) body)
            | _ -> assert false
        end
    | Primitive "Map", [f; xs] ->
        begin
          match f with
            | Closure (([x], body), closure_env) ->
                let env = env ++ closure_env in
                  reduce_for_source
                    (xs, fun v -> Singleton (computation (bind env (x, v)) body))
            | _ -> assert false
        end
    | Primitive "SortBy", [f; xs] ->
        begin
          match xs with
            | Concat [] -> Concat []
            | _ ->
                let gs, os', body =
                  match xs with
                    | For (_, gs, os', body) -> gs, os', body
                    | Concat (_::_)
                    | Singleton _
                    | Table _ ->
                        (* I think we can omit the `Table case as it
                           can never occur *)
                        (* eta-expand *)
                        eta_expand_list xs
                    | _ -> assert false in
                let xs = For (None, gs, os', body) in
                  begin
                    match f with
                      | Closure (([x], os), closure_env) ->
                          let os =
                            let env = env ++ closure_env in
                              let o = computation (bind env (x, tail_of_t xs)) os in
                                match o with
                                  | Record fields ->
                                      List.rev (StringMap.fold (fun _ o os -> o::os) fields [])
                                  | _ -> assert false
                          in
                            For (None, gs, os @ os', body)
                      | _ -> assert false
                  end
        end
    | Primitive "not", [v] ->
      reduce_not (v)
    | Primitive "&&", [v; w] ->
      reduce_and (v, w)
    | Primitive "||", [v; w] ->
      reduce_or (v, w)
    | Primitive "==", [v; w] ->
      reduce_eq (v, w)
    | Primitive f, args ->
        Apply (f, args)
    | If (c, t, e), args ->
        reduce_if_condition (c, apply env (t, args), apply env (e, args))
    | Apply (f, args), args' ->
        Apply (f, args @ args')
    | _ -> eval_error "Application of non-function"
  and computation env (binders, tailcomp) : Q.t =
    let open Ir in
    match binders with
      | [] -> tail_computation env tailcomp
      | b::bs ->
          begin
            match b with
              | Let (xb, (_, tc)) ->
                  let x = Var.var_of_binder xb in
                    computation (bind env (x, tail_computation env tc)) (bs, tailcomp)
              | Fun (_, _, _, (Location.Client | Location.Native)) ->
                  eval_error "Client function"
              | Fun ((f, _), _, _, _) ->
                (* This should never happen now that we have closure conversion*)
                raise (internal_error
                  ("Function definition in query: " ^ string_of_int f ^
                   ". This should have been closure-converted."))
              | Rec _ ->
                  eval_error "Recursive function"
              | Alien _ -> (* just skip it *)
                  computation env (bs, tailcomp)
              | Module _ -> raise (internal_error "Not implemented modules yet")
          end
  and tail_computation env : Ir.tail_computation -> Q.t = let open Ir in function
    | Return v -> value env v
    | Apply (f, args) ->
        apply env (value env f, List.map (value env) args)
    | Special (Ir.Query (None, e, _)) -> computation env e
    | Special (Ir.Table (db, name, keys, (readtype, _, _))) as _s ->
       (** WR: this case is because shredding needs to access the keys of tables
           but can we avoid it (issue #432)? *)
       (* Copied almost verbatim from evalir.ml, which seems wrong, we should probably call into that. *)
       begin
         match value env db, value env name, value env keys, (TypeUtils.concrete_type readtype) with
         | Q.Database (db, params), name, keys, `Record row ->
        let unboxed_keys =
          List.map
        (fun key ->
         List.map unbox_string (unbox_list key))
        (unbox_list keys)
        in
            Q.Table ((db, params), unbox_string name, unboxed_keys, row)
         | _ -> eval_error "Error evaluating table handle"
       end
    | Special _s ->
      (* FIXME:

         There's no particular reason why we can't allow
         database declarations in query blocks. (However, we do still
         have the problem that we currently have no way of enforcing
         that only one database be used inside a query block - see
         SML#.)  *)
      raise (Errors.runtime_error "special not allowed in query block")
    | Case (v, cases, default) ->
      let rec reduce_case (v, cases, default) =
        match v with
          | Q.Variant (label, v) as w ->
            begin
              match StringMap.lookup label cases, default with
                | Some ((x, _), c), _ ->
                  computation (bind env (x, v)) c
                | None, Some ((z, _), c) ->
                  computation (bind env (z, w)) c
                | None, None -> eval_error "Pattern matching failed"
            end
          | Q.If (c, t, e) ->
            Q.If
              (c,
               reduce_case (t, cases, default),
               reduce_case (e, cases, default))
          |  _ -> assert false
      in
        reduce_case (value env v, cases, default)
    | If (c, t, e) ->
      let c = value env c in
      let t = computation env t in
      let e = computation env e in
        reduce_if_condition (c, t, e)
  and reduce_concat vs =
    let vs =
      concat_map
        (function
          | Q.Concat vs -> vs
          | v -> [v])
        vs
    in
      match vs with
        | [v] -> v
        | vs -> Q.Concat vs
  and reduce_for_source : Q.t * (Q.t -> Q.t) -> Q.t =
    fun (source, body) ->
      let rs = fun source -> reduce_for_source (source, body) in
        let open Q in
        match source with
          | Singleton v -> body v
          | Concat vs ->
            reduce_concat (List.map rs vs)
          | If (c, t, Concat []) ->
            reduce_for_source
              (t, fun v -> reduce_where_then (c, body v))
          | For (_, gs, os, v) ->
            (* NOTE:

               We are relying on peculiarities of the way we manage
               the environment in order to avoid having to
               augment it with the generator bindings here.

               In particular, we rely on the fact that if a variable
               is not found on a lookup then we return the eta
               expansion of that variable rather than complaining that
               it isn't bound in the environment.

            *)
            reduce_for_body (gs, os, rs v)
          | Table table ->
            let field_types = table_field_types table in
            (* we need to generate a fresh variable in order to
               correctly handle self joins *)
            let x = Var.fresh_raw_var () in
              (* Debug.print ("fresh variable: " ^ string_of_int x); *)
              reduce_for_body ([(x, source)], [], body (Var (x, field_types)))
          | v -> eval_error "Bad source in for comprehension: %s" (string_of_t v)
  and reduce_for_body (gs, os, body) =
    match body with
      | Q.For (_, gs', os', body') -> Q.For (None, gs @ gs', os @ os', body')
      | _                         -> Q.For (None, gs, os, body)
  and reduce_if_condition (c, t, e) =
    let open Q in
    match c with
      | Constant (Constant.Bool true) -> t
      | Constant (Constant.Bool false) -> e
      | If (c', t', _) ->
        reduce_if_body
          (reduce_or (reduce_and (c', t'),
                      reduce_and (reduce_not c', t')),
           t,
           e)
      | _ ->
        if is_list t then
          if e = nil then
            reduce_where_then (c, t)
          else
            reduce_concat [reduce_where_then (c, t);
                           reduce_where_then (reduce_not c, e)]
        else
          reduce_if_body (c, t, e)
  and reduce_where_then (c, t) =
    let open Q in
    match t with
      (* optimisation *)
      | Constant (Constant.Bool true) -> t
      | Constant (Constant.Bool false) -> Concat []

      | Concat vs ->
        reduce_concat (List.map (fun v -> reduce_where_then (c, v)) vs)
      | For (_, gs, os, body) ->
        For (None, gs, os, reduce_where_then (c, body))
      | If (c', t', Concat []) ->
        reduce_where_then (reduce_and (c, c'), t')
      | _ ->
        If (c, t, Concat [])
  and reduce_if_body (c, t, e) =
    let open Q in
    match t with
      | Record then_fields ->
        begin match e with
          | Record else_fields ->
            assert (StringMap.equal (fun _ _ -> true) then_fields else_fields);
            Record
              (StringMap.fold
                 (fun name t fields ->
                   let e = StringMap.find name else_fields in
                     StringMap.add name (reduce_if_body (c, t, e)) fields)
                 then_fields
                 StringMap.empty)
          (* NOTE: this relies on any record variables having
             been eta-expanded by this point *)
          | _ -> eval_error "Mismatched fields"
        end
      | _ ->
        begin
          match t, e with
            | Constant (Constant.Bool true), _ ->
              reduce_or (c, e)
            | _, Constant (Constant.Bool false) ->
              reduce_and (c, t)
            | _ ->
              If (c, t, e)
        end
  (* simple optimisations *)
  and reduce_and (a, b) =
    let open Q in
    match a, b with
      | Constant (Constant.Bool true), x
      | x, Constant (Constant.Bool true)
      | (Constant (Constant.Bool false) as x), _
      | _, (Constant (Constant.Bool false) as x) -> x
      | _ -> Apply ("&&", [a; b])
  and reduce_or (a, b) =
    let open Q in
    match a, b with
      | (Constant (Constant.Bool true) as x), _
      | _, (Constant (Constant.Bool true) as x)
      | Constant (Constant.Bool false), x
      | x, Constant (Constant.Bool false) -> x
      | _ -> Apply ("||", [a; b])
  and reduce_not a =
    let open Q in
    match a with
      | Constant (Constant.Bool false) -> Constant (Constant.Bool true)
      | Constant (Constant.Bool true)  -> Constant (Constant.Bool false)
      | _                       -> Apply ("not", [a])
  and reduce_eq (a, b) =
    let open Q in
    let bool x = Constant (Constant.Bool x) in
    let eq_constant =
      function
        | (Constant.Bool a  , Constant.Bool b)   -> bool (a = b)
        | (Constant.Int a   , Constant.Int b)    -> bool (a = b)
        | (Constant.Float a , Constant.Float b)  -> bool (a = b)
        | (Constant.Char a  , Constant.Char b)   -> bool (a = b)
        | (Constant.String a, Constant.String b) -> bool (a = b)
        | (a, b)                 -> Apply ("==", [Constant a; Constant b])
    in
      match a, b with
        | (Constant a, Constant b) -> eq_constant (a, b)
        | (Variant (s1, a), Variant (s2, b)) ->
          if s1 <> s2 then
            Constant (Constant.Bool false)
          else
            reduce_eq (a, b)
        | (Record lfields, Record rfields) ->
          List.fold_right2
            (fun (_, v1) (_, v2) e ->
              reduce_and (reduce_eq (v1, v2), e))
            (StringMap.to_alist lfields)
            (StringMap.to_alist rfields)
            (Constant (Constant.Bool true))
        | (a, b) -> Apply ("==", [a; b])

  let eval env e =
(*    Debug.print ("e: "^Ir.show_computation e); *)
    computation (env_of_value_env env) e
end


(* convert a regexp to a like if possible *)
let rec likeify v =
  let open Q in
  let quote = Str.global_replace (Str.regexp_string "%") "\\%" in
    match v with
      | Variant ("Repeat", pair) ->
          begin
            match unbox_pair pair with
              | Variant ("Star", _), Variant ("Any", _) -> Some ("%")
              | _ -> None
          end
      | Variant ("Simply", Constant (Constant.String s)) -> Some (quote s)
      | Variant ("Quote", Variant ("Simply", v)) ->
          (* TODO:

             detect variables and convert to a concatenation operation
             (this needs to happen in RLIKE compilation as well)
          *)
         let rec string =
            function
              | Constant (Constant.String s) -> Some s
              | Singleton (Constant (Constant.Char c)) -> Some (string_of_char c)
              | Concat vs ->
                  let rec concat =
                    function
                      | [] -> Some ""
                      | v::vs ->
                          begin
                            match string v with
                              | None -> None
                              | Some s ->
                                  begin
                                    match concat vs with
                                      | None -> None
                                      | Some s' -> Some (s ^ s')
                                  end
                          end
                  in
                    concat vs
              | _ -> None
          in
            opt_map quote (string v)
      | Variant ("Seq", rs) ->
          let rec seq =
            function
              | [] -> Some ""
              | r::rs ->
                  begin
                    match likeify r with
                      | None -> None
                      | Some s ->
                          begin
                            match seq rs with
                              | None -> None
                              | Some s' -> Some (s^s')
                          end
                  end
          in
            seq (unbox_list rs)
      | Variant ("StartAnchor", _) -> Some ""
      | Variant ("EndAnchor", _) -> Some ""
      | _ -> assert false

let rec select_clause : Sql.index -> bool -> Q.t -> Sql.select_clause =
  fun index unit_query v ->
  (*  Debug.print ("select_clause: "^string_of_t v); *)
  let open Q in
  match v with
    | Concat _ -> assert false
    | For (_, [], _, body) ->
        select_clause index unit_query body
    | For (_, (x, Table (_db, table, _keys, _row))::gs, os, body) ->
        let body = select_clause index unit_query (For (None, gs, [], body)) in
        let os = List.map (base index) os in
          begin
            match body with
              | (fields, tables, condition, []) ->
                  (fields, (table, x)::tables, condition, os)
              | _ -> assert false
          end
    | If (c, body, Concat []) ->
      (* Turn conditionals into where clauses. We might want to do
         this earlier on.  *)
      let c = base index c in
      let (fields, tables, c', os) = select_clause index unit_query body in
      let c = Sql.smart_and c c' in
      (fields, tables, c, os)
    | Table (_db, table, _keys, (fields, _, _)) ->
      (* eta expand tables. We might want to do this earlier on.  *)
      (* In fact this should never be necessary as it is impossible
         to produce non-eta expanded tables. *)
      let var = Sql.fresh_table_var () in
      let fields =
        List.rev
          (StringMap.fold
             (fun name _ fields ->
               (Sql.Project (var, name), name)::fields)
             fields
             [])
      in
        (fields, [(table, var)], Sql.Constant (Constant.Bool true), [])
    | Singleton _ when unit_query ->
      (* If we're inside an Sql.Empty or a Sql.Length it's safe to ignore
         any fields here. *)
      (* We currently detect this earlier, so the unit_query stuff here
         is redundant. *)
      ([], [], Sql.Constant (Constant.Bool true), [])
    | Singleton (Record fields) ->
      let fields =
        List.rev
          (StringMap.fold
             (fun name v fields ->
               (base index v, name)::fields)
             fields
             [])
      in
        (fields, [], Sql.Constant (Constant.Bool true), [])
    | _ -> assert false
and clause : Sql.index -> bool -> Q.t -> Sql.query =
  fun index unit_query v -> Sql.Select(select_clause index unit_query v)
and base : Sql.index -> Q.t -> Sql.base = fun index ->
  let open Q in
  function
    | If (c, t, e) ->
      Sql.Case (base index c, base index t, base index e)
    | Apply ("tilde", [s; r]) ->
      begin
        match likeify r with
          | Some r ->
            Sql.Apply ("LIKE", [base index s; Sql.Constant (Constant.String r)])
          | None ->
            let r =
                  (* HACK:

                     this only works if the regexp doesn't include any variables bound by the query
                  *)
                  Sql.Constant (Constant.String (Regex.string_of_regex (Linksregex.Regex.ofLinks (value_of_expression r))))
                in
                  Sql.Apply ("RLIKE", [base index s; r])
        end
    | Apply ("Empty", [v]) ->
        Sql.Empty (unit_query v)
    | Apply ("length", [v]) ->
        Sql.Length (unit_query v)
    | Apply (f, vs) ->
        Sql.Apply (f, List.map (base index) vs)
    | Project (Var (x, _field_types), name) ->
        Sql.Project (x, name)
    | Constant c -> Sql.Constant c
    | Primitive "index" ->
        (* This is the only place the index parameter is ever materially used. *)
        Sql.RowNumber index
    | e ->
      Debug.print ("Not a base expression: " ^ Q.show e);
      assert false

and unit_query v =
  let prepare_clauses : Q.t -> Q.t list =
    function
      | Q.Concat vs -> vs
      | v -> [v]
  in
  (* queries passed to Empty and Length
     (where we don't care about what data they return)
  *)
  Sql.UnionAll (List.map (clause [] true) (prepare_clauses v), 0)
and sql_of_query v =
  clause [] false v

(* The following code is specific to nested queries *)
(* The index parameter is essentially a free variable in the query
   that can only be replaced by Sql.RowNumber index.
   It would be nice to be able to remove this parameter and just
   substitute the SQL RowNumber expression when we generate SQL.
   Then the following nesting-specific code could live somewhere else, such as
   evalNestedQuery. *)

type let_clause = Var.var * Q.t * Var.var * Q.t
type let_query = let_clause list


let gens_index (gs : (Var.var * Q.t) list)   =
  let all_fields t =
    let field_types = table_field_types t in
    labels_of_field_types field_types
  in
 (* Use keys if available *)
  let key_fields t =
    match t with
      (_, _, (ks::_), _) -> StringSet.from_list ks
    |    _ -> all_fields t
  in
  let table_index get_fields (x, source) =
    let t = match source with Q.Table t -> t | _ -> assert false in
    let labels = get_fields t in
      List.rev
        (StringSet.fold
           (fun name ps -> (x, name) :: ps)
           labels
           [])
  in
  let get_fields = if Settings.get_value Basicsettings.use_keys_in_shredding
                   then key_fields
                   else all_fields
  in concat_map (table_index get_fields) gs

let outer_index gs_out = gens_index gs_out
let inner_index z gs_in =
  (* it's just a dynamic index! *)
  (z, "2") :: gens_index gs_in

let extract_gens =
  function
    | Q.For (_, gs, _, _) -> gs
    | _ -> assert false

let let_clause : let_clause -> Sql.query =
  fun (q, outer, z, inner) ->
    let gs_out = extract_gens outer in
    let gs_in = extract_gens inner in
      Sql.With (q,
             clause (outer_index gs_out) false outer,
             z,
             clause (inner_index z gs_in) false inner)

let sql_of_let_query : let_query -> Sql.query =
  fun cs ->
    Sql.UnionAll (List.map (let_clause) cs, 0)

let update : Value.database -> ((Ir.var * string) * Q.t option * Q.t) -> string =
  fun db ((_, table), where, body) ->
    Sql.reset_dummy_counter ();
    let base = (base []) ->- (Sql.string_of_base db true) in
    let where =
      match where with
        | None -> ""
        | Some where ->
            " where (" ^ base where ^ ")" in
    let fields =
      match body with
        | Q.Record fields ->
            String.concat ","
              (List.map
                (fun (label, v) -> db#quote_field label ^ " = " ^ base v)
                (StringMap.to_alist fields))
        | _ -> assert false
    in
      "update "^table^" set "^fields^where

let delete : Value.database -> ((Ir.var * string) * Q.t option) -> string =
  fun db ((_, table), where) ->
  Sql.reset_dummy_counter ();
  let base = base [] ->- (Sql.string_of_base db true) in
  let where =
    match where with
      | None -> ""
      | Some where ->
          " where (" ^ base where ^ ")"
  in
    "delete from "^table^where

let compile_update : Value.database -> Value.env ->
  ((Ir.var * string * Types.datatype StringMap.t) * Ir.computation option * Ir.computation) -> string =
  fun db env ((x, table, field_types), where, body) ->
    let env = Eval.bind (Eval.env_of_value_env env) (x, Q.Var (x, field_types)) in
(*      let () = opt_iter (fun where ->  Debug.print ("where: "^Ir.show_computation where)) where in*)
    let where = opt_map (Eval.computation env) where in
(*       Debug.print ("body: "^Ir.show_computation body); *)
    let body = Eval.computation env body in
    let q = update db ((x, table), where, body) in
      Debug.print ("Generated update query: "^q);
      q

let compile_delete : Value.database -> Value.env ->
  ((Ir.var * string * Types.datatype StringMap.t) * Ir.computation option) -> string =
  fun db env ((x, table, field_types), where) ->
    let env = Eval.bind (Eval.env_of_value_env env) (x, Q.Var (x, field_types)) in
    let where = opt_map (Eval.computation env) where in
    let q = delete db ((x, table), where) in
      Debug.print ("Generated update query: "^q);
      q
